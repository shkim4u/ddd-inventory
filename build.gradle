import com.epages.restdocs.apispec.gradle.OpenApi3Task
import io.swagger.v3.oas.models.servers.Server

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.1' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
    id 'com.github.davidmc24.gradle.plugin.avro' version '1.3.0' apply false
    id 'com.epages.restdocs-api-spec' version '0.18.4' apply false     // Spring RestDocs + OpenAPI 3 (Swagger)
}

allprojects {
    group = 'com.legmod.ddd'
    version = '0.0.1-SNAPSHOT'
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    ext {
        set('avroVersion', "1.12.0")
    }

    bootJar.enabled = false
    jar.enabled = true

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
    }
}

project(':domain') {
    apply plugin: 'com.github.davidmc24.gradle.plugin.avro'

    tasks.register('submoduleUpdate', Exec) {
        description 'Update git submodules for message-idl'
        commandLine 'git', 'submodule', 'update', '--remote', '--recursive'
    }

    generateAvroJava {
        dependsOn 'submoduleUpdate'
        source("${rootProject.projectDir}/message-idl/avro")
        outputDir = file("${projectDir}/src/main/java")
    }

    dependencies {
        // Spring Data JPA.
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        // Avro.
        implementation "org.apache.avro:avro:${avroVersion}"
    }
}

project(':application') {
    dependencies {
        implementation project(':domain')
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    }
}

project(':interface') {
    apply plugin: 'com.epages.restdocs-api-spec'
    ext {
        set('springCloudVersion', "2024.0.0")
    }

    bootJar {
        enabled = true
        archiveFileName = 'inventory.jar'
    }
    jar.enabled = false

    tasks.named('test') {
        useJUnitPlatform()
    }

//    tasks.register('openapi3', OpenApi3Task) {
//        servers = [
//                [url: 'http://localhost:8080'],
//                [url: 'http://OPENAPI3_URL_TO_REPLACE']
//        ] as List<? extends Server>
//        title = 'Inventory service API'
//        description = 'Inventory service API description'
//        version = '1.0.0'
//        format = 'json'
////        apiVersion = '1.0.0'  // Set the apiVersion property
////        outputDirectory = layout.buildDirectory.file("/api-spec")  // Set the outputDirectory property
////        outputFileNamePrefix = 'openapi3'  // Set the outputFileNamePrefix property
//    }

    openapi3 {
        servers = [
                { url = 'http://localhost:8080' },
                { url = 'http://OPENAPI3_URL_TO_REPLACE' }
        ]
        title = 'Inventory service API'
        description = 'Inventory service API description'
        version = '1.0.0'
        format = 'json'
    }

    tasks.register('copyOasToSwagger', Copy) {
        doFirst {
            delete 'src/main/resources/static/openapi3.json'
        }
//        from "${buildDir}/api-spec/openapi3.json"
        from layout.buildDirectory.file("api-spec/openapi3.json")
        into 'src/main/resources/static/.'
        dependsOn 'openapi3'
    }

    dependencies {
        implementation project(':domain')
        implementation project(':application')
        runtimeOnly project(':infrastructure')
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        // Spring Cloud.
        implementation 'org.springframework.cloud:spring-cloud-starter'
        // Spring Cloud Stream and RabbitMQ.
        // spring-cloud-starter-stream-rabbit = spring-cloud-stream-binder-rabbit + spring-cloud-stream
        implementation 'org.springframework.cloud:spring-cloud-stream'
        implementation 'org.springframework.cloud:spring-cloud-stream-binder-rabbit'
        testImplementation 'org.springframework.cloud:spring-cloud-stream-test-support'
        // Avro.
        implementation "org.apache.avro:avro:${avroVersion}"
        // MapStruct.
        implementation 'org.mapstruct:mapstruct:1.5.2.Final'
        annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.2.Final'
        // Spring RestDocs + OpenAPI 3 (Swagger).
        testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
        testImplementation 'com.epages:restdocs-api-spec-mockmvc:0.18.4'
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }
}

project(':infrastructure') {
    dependencies {
        implementation project(':domain')
        // Flyway.
        implementation 'org.flywaydb:flyway-core'
        implementation 'org.flywaydb:flyway-database-postgresql'
        // PostgreSQL.
        runtimeOnly 'org.postgresql:postgresql'
    }
}
